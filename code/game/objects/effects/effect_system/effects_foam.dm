// Foam
// Similar to smoke, but slower and mobs absorb its reagent through their exposed skin.
#define ALUMINUM_FOAM 1
#define IRON_FOAM 2
#define RESIN_FOAM 3


/// Во сколько раз медленная фаза пены тикает реже быстрой (SSprocessing 1с / SSfastprocess 0.2с).
/// Доза химии и расход жизни масштабируются этим же множителем - суммарный эффект как раньше.
#define FOAM_SLOW_TICK_MULTIPLIER 5

/obj/effect/particle_effect/foam
	name = "foam"
	icon_state = "foam"
	opacity = 0
	anchored = TRUE
	density = FALSE
	layer = EDGED_TURF_LAYER
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	var/amount = 10
	animate_movement = 0
	var/metal = 0
	var/lifetime = 40
	var/reagent_divisor = 7
	/// TRUE = разлив закончен, пена дотикивает на медленном SSprocessing.
	var/slow_processing = FALSE
	/// FALSE = пене нужен быстрый тик всю жизнь (пожарная пена жрёт хотспоты на 5 Гц).
	var/allow_slow_processing = TRUE
	/// TRUE = химия турфу и предметам копится тиками и выдаётся одной дозой (см. process()).
	var/batch_reagent_doses = FALSE
	/// Тиков накоплено с прошлой реакции; -1 = пена ещё не тикала, первый тик реагирует сразу.
	var/react_ticks = -1
	var/static/list/blacklisted_turfs = typecacheof(list(
	/turf/open/space/transit,
	/turf/open/chasm,
	/turf/open/lava))

/obj/effect/particle_effect/foam/firefighting
	name = "firefighting foam"
	lifetime = 20 //doesn't last as long as normal foam
	amount = 0 //no spread
	allow_slow_processing = FALSE // тушение требует ловить хотспоты каждый быстрый тик
	var/absorbed_plasma = 0

/obj/effect/particle_effect/foam/firefighting/MakeSlippery()
	return

/obj/effect/particle_effect/foam/firefighting/process()
	..()

	var/turf/open/T = get_turf(src)
	var/obj/effect/hotspot/hotspot = (locate(/obj/effect/hotspot) in T)
	if(hotspot && istype(T) && T.air)
		qdel(hotspot)
		var/datum/gas_mixture/G = T.air
		var/amt_removed = min(30,G.get_moles(GAS_PLASMA))  //Absorb some plasma
		G.adjust_moles(GAS_PLASMA,-amt_removed)
		absorbed_plasma += amt_removed
		var/list/fire_gases = GLOB.gas_data.fire_temperatures.Copy()
		for(var/gas in fire_gases - GAS_PLASMA)
			if(amt_removed <= 0)
				break
			var/this_amount = min(30-amt_removed, G.get_moles(gas))
			G.adjust_moles(gas, -this_amount)
			amt_removed -= this_amount
		if(G.return_temperature() > T20C)
			G.set_temperature(max(G.return_temperature()/2,T20C))
		T.air_update_turf()

/obj/effect/particle_effect/foam/firefighting/kill_foam()
	stop_processing()

	if(absorbed_plasma)
		var/obj/effect/decal/cleanable/plasma/P = (locate(/obj/effect/decal/cleanable/plasma) in get_turf(src))
		if(!P)
			P = new(loc)
		P.reagents.add_reagent(/datum/reagent/stable_plasma, absorbed_plasma)

	flick("[icon_state]-disolve", src)
	QDEL_IN(src, 5)

/obj/effect/particle_effect/foam/firefighting/foam_mob(mob/living/L)
	if(!istype(L))
		return
	L.adjust_fire_stacks(-2)
	L.ExtinguishMob()

/obj/effect/particle_effect/foam/firefighting/temperature_expose(datum/gas_mixture/air, exposed_temperature, exposed_volume)
	return

/obj/effect/particle_effect/foam/watertype
	lifetime = 120
	alpha = 96
	layer = MOB_LOWER_LAYER

/obj/effect/particle_effect/foam/metal
	name = "aluminium foam"
	metal = ALUMINUM_FOAM
	icon_state = "mfoam"

/obj/effect/particle_effect/foam/metal/MakeSlippery()
	return

/obj/effect/particle_effect/foam/metal/smart
	name = "smart foam"

/obj/effect/particle_effect/foam/metal/iron
	name = "iron foam"
	metal = IRON_FOAM

/obj/effect/particle_effect/foam/metal/resin
	name = "resin foam"
	metal = RESIN_FOAM

/obj/effect/particle_effect/foam/short_life
	lifetime = 1 SECONDS
	// Батчится любая химия, налитая в пену. Пороговые по объёму reaction_turf/reaction_obj
	// с укрупнённой дозы могут сработать там, где дольки не срабатывали: размен принят.
	batch_reagent_doses = TRUE

/obj/effect/particle_effect/foam/long_life
	lifetime = 30 SECONDS

/obj/effect/particle_effect/foam/Initialize(mapload)
	. = ..()
	MakeSlippery()
	create_reagents(1000, NONE, NO_REAGENTS_VALUE) //limited by the size of the reagent holder anyway.
	START_PROCESSING(SSfastprocess, src)
	playsound(src, 'sound/effects/bubbles2.ogg', 80, 1, -3)

/obj/effect/particle_effect/foam/proc/MakeSlippery()
	AddComponent(/datum/component/slippery, 100)

/obj/effect/particle_effect/foam/Destroy()
	stop_processing()
	return ..()


/obj/effect/particle_effect/foam/proc/stop_processing()
	if(!(datum_flags & DF_ISPROCESSING))
		return
	if(slow_processing)
		STOP_PROCESSING(SSprocessing, src)
	else
		STOP_PROCESSING(SSfastprocess, src)

/// Выдаёт недоданный остаток батча химии турфу и предметам; звать повторно безопасно.
/obj/effect/particle_effect/foam/proc/flush_batched_reagent_dose()
	if(!batch_reagent_doses || react_ticks <= 0)
		return
	var/spent_ticks = min(react_ticks, max(reagent_divisor, 1))
	react_ticks = 0
	if(!reagents?.total_volume)
		return
	apply_reagent_dose(spent_ticks / max(reagent_divisor, 1), get_turf(src))

/obj/effect/particle_effect/foam/proc/kill_foam()
	stop_processing()
	flush_batched_reagent_dose()
	switch(metal)
		if(ALUMINUM_FOAM)
			new /obj/structure/foamedmetal(get_turf(src))
		if(IRON_FOAM)
			new /obj/structure/foamedmetal/iron(get_turf(src))
		if(RESIN_FOAM)
			new /obj/structure/foamedmetal/resin(get_turf(src))
	flick("[icon_state]-disolve", src)
	QDEL_IN(src, 5)

/obj/effect/particle_effect/foam/smart/kill_foam() //Smart foam adheres to area borders for walls
	stop_processing()
	flush_batched_reagent_dose()
	if(metal)
		var/turf/T = get_turf(src)
		if(isspaceturf(T)) //Block up any exposed space
			T.PlaceOnTop(/turf/open/floor/plating/foam, flags = CHANGETURF_INHERIT_AIR)
		for(var/direction in GLOB.cardinals)
			var/turf/cardinal_turf = get_step(T, direction)
			if(get_base_area(cardinal_turf) != get_area(T)) //We're at an area boundary, so let's block off this turf!
				new/obj/structure/foamedmetal(T)
				break
	flick("[icon_state]-disolve", src)
	QDEL_IN(src, 5)

/obj/effect/particle_effect/foam/process()
	// В медленной фазе тик приходит в FOAM_SLOW_TICK_MULTIPLIER раз реже -
	// расход жизни и доза химии масштабируются, суммарный эффект прежний.
	var/tick_multiplier = slow_processing ? FOAM_SLOW_TICK_MULTIPLIER : 1
	var/divisor = max(reagent_divisor, 1)
	lifetime -= tick_multiplier
	if(lifetime < 1)
		kill_foam()
		return

	// range(0, src) строил список из турфа и его содержимого ДВАЖДЫ за тик на каждую
	// пену: при пожаротушении это 5427 пен в одном проходе SSfastprocess (раунд 9859),
	// то есть десять тысяч лишних списков. Турф и так известен.
	var/turf/foam_turf = get_turf(src)

	// Обычная пена дозирует долю каждый рабочий тик, короткоживущая (batch_reagent_doses)
	// копит тики и выдаёт их одной дозой, а остаток отдаёт на смерти: сумма за жизнь та же.
	var/react_fraction = 0
	var/working_tick = lifetime % divisor
	if(batch_reagent_doses)
		var/first_react = react_ticks < 0 // первый тик реагирует сразу
		react_ticks = max(react_ticks, 0) + (working_tick ? tick_multiplier : 0)
		if((first_react && react_ticks) || react_ticks >= divisor)
			var/spent_ticks = min(react_ticks, divisor)
			react_fraction = spent_ticks / divisor
			react_ticks -= spent_ticks
	else if(working_tick)
		react_fraction = tick_multiplier / divisor

	// У пожарной и металлической пены холдер пуст: обходить содержимое турфа незачем.
	if(react_fraction && reagents?.total_volume)
		apply_reagent_dose(react_fraction, foam_turf)
	var/hit = 0
	if(foam_turf)
		for(var/mob/living/L in foam_turf)
			hit += foam_mob(L, tick_multiplier)
	if(hit)
		lifetime += tick_multiplier //this is so the decrease from mobs hit and the natural decrease don't cumulate.

	if(--amount < 0)
		// Разлив закончен: пена больше не спредится, дотикивать жизнь и травить
		// стоящих в ней можно на медленном процессинге. Именно одновременность
		// тысяч пен на быстром тике давала 228мс/проход SSfastprocess (раунд 9746,
		// Scrubber Overflow), при этом спред - первые секунды жизни каждой пены.
		if(!slow_processing && allow_slow_processing)
			slow_processing = TRUE
			STOP_PROCESSING(SSfastprocess, src)
			START_PROCESSING(SSprocessing, src)
		return
	spread_foam()

/// Одна доза химии турфу и его содержимому: общая для рабочего тика и для остатка батча.
/obj/effect/particle_effect/foam/proc/apply_reagent_dose(react_fraction, turf/foam_turf)
	if(!react_fraction || !foam_turf)
		return
	for(var/obj/O in foam_turf)
		if(O.type == src.type)
			continue
		if(isturf(O.loc))
			var/turf/T = O.loc
			if((T.turf_flags & TURF_INTACT) && O.level == 1) //hidden under the floor
				continue
		reagents.reaction(O, TOUCH, react_fraction)
	reagents.reaction(foam_turf, TOUCH, react_fraction)

/obj/effect/particle_effect/foam/proc/foam_mob(mob/living/L, tick_multiplier = 1)
	if(lifetime<1)
		return FALSE
	if(!istype(L))
		return FALSE
	var/divisor = max(reagent_divisor, 1)
	if(lifetime % divisor)
		reagents.reaction(L, TOUCH, tick_multiplier / divisor)
	lifetime -= tick_multiplier
	return TRUE

/obj/effect/particle_effect/foam/proc/spread_foam()
	var/turf/t_loc = get_turf(src)
	if(!t_loc) // пену могли убрать из мира (kill_foam/подбор) между постановкой в очередь и спредом
		return
	var/list/adjacent_turfs = t_loc.atmos_adjacent_turfs
	var/copied_adjacency = FALSE
	for(var/adjacent_index in 1 to length(adjacent_turfs))
		var/turf/T = adjacent_turfs[adjacent_index]
		var/obj/effect/particle_effect/foam/foundfoam = locate() in T //Don't spread foam where there's already foam!
		if(foundfoam)
			continue

		if(is_type_in_typecache(T, blacklisted_turfs))
			continue

		// Реакция на мобе может изменить соседство; до неё снимок не нужен.
		if(!copied_adjacency)
			adjacent_turfs = adjacent_turfs.Copy()
			copied_adjacency = TRUE
		for(var/mob/living/L in T)
			foam_mob(L)
		var/obj/effect/particle_effect/foam/F = new src.type(T)
		F.amount = amount
		reagents.copy_to(F, (reagents.total_volume))
		F.add_atom_colour(color, FIXED_COLOUR_PRIORITY)
		F.metal = metal


/obj/effect/particle_effect/foam/temperature_expose(datum/gas_mixture/air, exposed_temperature, exposed_volume)
	if(prob(max(0, exposed_temperature - 475))) //foam dissolves when heated
		kill_foam()


/obj/effect/particle_effect/foam/metal/temperature_expose(datum/gas_mixture/air, exposed_temperature, exposed_volume)
	return


///////////////////////////////////////////////
//FOAM EFFECT DATUM
/datum/effect_system/foam_spread
	var/amount = 25		// the size of the foam spread.
	/// Носитель химии до момента рождения пены. Голый /obj, у которого reagents.my_atom
	/// смотрит обратно на него же - ссылочный цикл, а рефкаунт BYOND циклы не разбирает
	/// никогда. Разорвать его может только Destroy(), то есть qdel самой системы.
	var/obj/chemholder
	effect_type = /obj/effect/particle_effect/foam
	var/metal = 0
	// Система одноразовая и убирает себя сама в конце start() - ровно та же болезнь и то же
	// лечение, что у /datum/effect_system/smoke_spread/chem, подробный разбор там. Коротко:
	// ни одно место создания пены qdel не звало, поэтому каждый разлив оставлял в мире
	// навсегда голый /obj + /datum/reagents(1000); перепись прода 10050/10052/10054 видела
	// это как непрерывный рост числа голых /obj. Цена - экземпляр не переиспользуем; ни
	// одного foam_spread в переменной объекта в дереве нет, все места создания локальные.
	autocleanup = TRUE

/datum/effect_system/foam_spread/watertype                      //Для ситуаций, когда требуется якобы потоп
	effect_type = /obj/effect/particle_effect/foam/watertype

/datum/effect_system/foam_spread/metal
	effect_type = /obj/effect/particle_effect/foam/metal


/datum/effect_system/foam_spread/metal/smart
	effect_type = /obj/effect/particle_effect/foam/smart

/datum/effect_system/foam_spread/short
	effect_type = /obj/effect/particle_effect/foam/short_life

/datum/effect_system/foam_spread/long
	effect_type = /obj/effect/particle_effect/foam/long_life

/datum/effect_system/foam_spread/New()
	..()
	chemholder = new /obj()
	var/datum/reagents/R = new/datum/reagents(1000)
	chemholder.reagents = R
	R.my_atom = chemholder

/datum/effect_system/foam_spread/Destroy()
	qdel(chemholder)
	chemholder = null
	return ..()

/datum/effect_system/foam_spread/set_up(amt=5, loca, datum/reagents/carry = null)
	if(isturf(loca))
		location = loca
	else
		location = get_turf(loca)

	amount = round(sqrt(amt / 2), 1)
	carry.copy_to(chemholder, carry.total_volume)

/datum/effect_system/foam_spread/metal/set_up(amt=5, loca, datum/reagents/carry = null, metaltype)
	..()
	metal = metaltype

/datum/effect_system/foam_spread/start()
	// Система себя уже убрала (см. autocleanup у типа) - chemholder отпущен, дальше идти
	// некуда. Гард такой же, как у /datum/effect_system/start().
	if(QDELETED(src))
		return
	var/obj/effect/particle_effect/foam/F = new effect_type(location)
	var/foamcolor = mix_color_from_reagents(chemholder.reagents.reagent_list)
	chemholder.reagents.copy_to(F, chemholder.reagents.total_volume/amount)
	F.add_atom_colour(foamcolor, FIXED_COLOUR_PRIORITY)
	F.amount = amount
	F.metal = metal

	// Химия отдана рождённой пене, chemholder больше не нужен ни на что.
	if(autocleanup)
		qdel(src)


//////////////////////////////////////////////////////////
// FOAM STRUCTURE. Formed by metal foams. Dense and opaque, but easy to break
/obj/structure/foamedmetal
	icon = 'icons/effects/effects.dmi'
	icon_state = "metalfoam"
	density = TRUE
	opacity = 1 	// changed in New()
	anchored = TRUE
	layer = EDGED_TURF_LAYER
	resistance_flags = FIRE_PROOF | ACID_PROOF
	name = "foamed metal"
	desc = "A lightweight foamed metal wall."
	gender = PLURAL
	max_integrity = 20
	CanAtmosPass = ATMOS_PASS_DENSITY
	attack_hand_speed = CLICK_CD_MELEE
	attack_hand_is_action = TRUE

/obj/structure/foamedmetal/Initialize(mapload)
	. = ..()
	air_update_turf(TRUE)

/obj/structure/foamedmetal/Move()
	var/turf/T = loc
	. = ..()
	move_update_air(T)

/obj/structure/foamedmetal/attack_paw(mob/user)
	return attack_hand(user)

/obj/structure/foamedmetal/play_attack_sound(damage_amount, damage_type = BRUTE, damage_flag = 0)
	playsound(src.loc, 'sound/weapons/tap.ogg', 100, 1)

/obj/structure/foamedmetal/on_attack_hand(mob/user, act_intent = user.a_intent, unarmed_attack_flags)
	user.do_attack_animation(src, ATTACK_EFFECT_PUNCH)
	to_chat(user, "<span class='warning'>You hit [src] but bounce off it!</span>")
	playsound(src.loc, 'sound/weapons/tap.ogg', 100, 1)

/obj/structure/foamedmetal/iron
	max_integrity = 50
	icon_state = "ironfoam"

//Atmos Backpack Resin, transparent, prevents atmos and filters the air
/obj/structure/foamedmetal/resin
	name = "\improper ATMOS Resin"
	desc = "A lightweight, transparent resin used to suffocate fires, scrub the air of toxins, and restore the air to a safe temperature."
	opacity = FALSE
	icon_state = "atmos_resin"
	alpha = 120
	max_integrity = 10
	pass_flags_self = PASSGLASS

/obj/structure/foamedmetal/resin/Initialize(mapload)
	. = ..()
	neutralize_air()
	addtimer(CALLBACK(src, PROC_REF(neutralize_air)), 5)		// yeah this sucks, maybe when auxmos is out

/obj/structure/foamedmetal/resin/proc/neutralize_air()
	if(isopenturf(loc))
		var/turf/open/O = loc
		O.ClearWet()
		if(O.air)
			var/datum/gas_mixture/G = O.air
			G.set_temperature(293.15)
			for(var/obj/effect/hotspot/H in O)
				qdel(H)
			for(var/I in G.get_gases())
				if(I == GAS_O2 || I == GAS_N2)
					continue
				G.set_moles(I, 0)
			O.air_update_turf()
		for(var/obj/machinery/atmospherics/components/unary/U in O)
			if(!U.welded)
				U.welded = TRUE
				U.update_icon()
				U.visible_message("<span class='danger'>[U] sealed shut!</span>")
		for(var/mob/living/L in O)
			L.ExtinguishMob()
		for(var/obj/item/Item in O)
			Item.extinguish()

#undef ALUMINUM_FOAM
#undef IRON_FOAM
#undef RESIN_FOAM
#undef FOAM_SLOW_TICK_MULTIPLIER
