// Populate the space level list and prepare space transitions
/datum/controller/subsystem/mapping/proc/InitializeDefaultZLevels()
	if (z_list)  // subsystem/Recover or badminnery, no need
		return

	z_list = list()
	var/list/default_map_traits = DEFAULT_MAP_TRAITS

	if (default_map_traits.len != world.maxz)
		WARNING("More or less map attributes pre-defined ([default_map_traits.len]) than existent z-levels ([world.maxz]). Ignoring the larger.")
		if (default_map_traits.len > world.maxz)
			default_map_traits.Cut(world.maxz + 1)

	for (var/I in 1 to default_map_traits.len)
		var/list/features = default_map_traits[I]
		//WHITE-STEEL PORT: первый default-уровень задаёт центр орбитальной карты (phobos).
		//Without it linked_map.center stays null and station/lavaland fail to orbit.
		var/datum/space_level/S
		if(I == 1)
			S = new(I, features[DL_NAME], features[DL_TRAITS], orbital_body_type = /datum/orbital_object/z_linked/phobos)
		else
			S = new(I, features[DL_NAME], features[DL_TRAITS])
		z_list += S
		calculate_z_level_gravity(I)

/datum/controller/subsystem/mapping/proc/add_new_zlevel(name, traits = list(), z_type = /datum/space_level, orbital_body_type)
	SEND_GLOBAL_SIGNAL(COMSIG_GLOB_NEW_Z, args)
	var/new_z = z_list.len + 1
	if (world.maxz < new_z)
		world.incrementMaxZ()
		CHECK_TICK
	// TODO: sleep here if the Z level needs to be cleared
	var/datum/space_level/S = new z_type(new_z, name, traits, orbital_body_type)
	z_list += S
	calculate_z_level_gravity(new_z)
	//z-уровни, созданные до инита грида, разложит SSspatial_grid/Initialize сам
	SSspatial_grid.propogate_spatial_grid_to_new_z(S)
	return S

/datum/controller/subsystem/mapping/proc/get_level(z) as /datum/space_level
	if (z_list && z >= 1 && z <= z_list.len)
		return z_list[z]
	CRASH("Unmanaged z-level [z]! maxz = [world.maxz], z_list.len = [z_list ? z_list.len : "null"]")

/// Пересчитать кэш гравитации z-уровня: max(setting) включённых генераторов,
/// иначе ZTRAIT_GRAVITY. Трейты после создания уровня не меняются, поэтому
/// инвалидация нужна только генераторам (update_list()).
/datum/controller/subsystem/mapping/proc/calculate_z_level_gravity(z_level_number)
	if(!isnum(z_level_number) || z_level_number < 1)
		return FALSE

	var/max_gravity = 0
	for(var/obj/machinery/gravity_generator/main/grav_gen as anything in GLOB.gravity_generators["[z_level_number]"])
		max_gravity = max(grav_gen.setting, max_gravity)

	max_gravity = max_gravity || level_trait(z_level_number, ZTRAIT_GRAVITY) || 0
	if(z_level_number > length(gravity_by_z_level))
		gravity_by_z_level.len = z_level_number
	gravity_by_z_level[z_level_number] = max_gravity
	return max_gravity
